apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def projectName = "myPlugin"
def mavenDesc = 'just is a test'
def baseUrl = 'https://github.com/gindoc'
def siteUrl = baseUrl
def gitUrl = "${baseUrl}/Groovy.git"
def issueUrl = "${baseUrl}/Groovy/issues"

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2017'

def username = 'gindoc'

// 配置maven库，生成POM.xml文件
install {
    repositories {
        mavenInstaller {
            // This generates POM.xml with proper parameters
            pom.project {
                // Description,项目描述，随便填
                name projectName
                description mavenDesc
                url siteUrl

                // Archive
                groupId project.group
                artifactId archivesBaseName
                version project.version

                // License
                inceptionYear inception
                licenses {
                    //开源协议
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }
                developers {
                    developer {
                        name username
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// 生成源文件
task sourcesJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

//上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    configurations = ['archives']
    pkg {
        //这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = 'maven'
        //发布到JCenter上的项目名字
        name = projectName
        //项目描述
        desc = mavenDesc
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        labels = ['gradle', 'plugin', 'time']
        licenses = licenseIds
        publish = true
        publicDownloadNumbers = true
    }
}

javadoc {
    options{
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}